include(CMakeParseArguments)

function(CMakeUtilsRemoveMatchesFromList)
  set(options)
  set(oneValueArgs)
  set(multiValueArgs MATCHES)
  cmake_parse_arguments(TQ_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (TQ_LIST ${TQ_PARSED_UNPARSED_ARGUMENTS})
    foreach (TQ_ENTRY ${${TQ_LIST}})
      foreach (TQ_MATCH ${TQ_PARSED_MATCHES})
        if (${TQ_ENTRY} MATCHES ${TQ_MATCH})
          list(REMOVE_ITEM ${TQ_LIST} ${TQ_ENTRY})
        endif()
      endforeach()
    endforeach()
    set(${TQ_LIST} ${${TQ_LIST}} PARENT_SCOPE)
  endforeach()
endfunction()

function(CMakeUtilsAddTestsFromSourceList)
  set(options)
  set(oneValueArgs PREFIX)
  set(multiValueArgs LINK_LIBS COMPILE_OPTIONS COMPILE_DEFINITIONS COMPILE_FEATURES)

  cmake_parse_arguments(TQ_PARSED "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})

  foreach (TQ_LIST ${TQ_PARSED_UNPARSED_ARGUMENTS})
    foreach (TQ_ENTRY ${${TQ_LIST}})
      if (${TQ_ENTRY} MATCHES ".*_test.cpp")
        string(REGEX REPLACE "^.*\/(.*)_test\.cpp$" "\\1" testName "${TQ_ENTRY}")
        set(testName "${TQ_PARSED_PREFIX}-${testName}-test")

        add_executable(${testName} ${TQ_ENTRY})
        target_compile_features(${testName} PRIVATE ${TQ_PARSED_COMPILE_FEATURES})
        target_compile_options(${testName} PRIVATE ${TQ_PARSED_COMPILE_OPTIONS})
        target_compile_definitions(${testName} PRIVATE ${TQ_PARSED_COMPILE_DEFINITIONS})
        target_link_libraries(${testName} PRIVATE ${TQ_PARSED_LINK_LIBS})

        add_test(${testName} ${testName})
      endif()
    endforeach()
  endforeach()
endfunction()

function(CMakeUtilsExcludeTestsFromSourceList TQ_LIST)
  list(FILTER ${TQ_LIST} EXCLUDE REGEX ".*_test.cpp")
  set(${TQ_LIST} ${${TQ_LIST}} PARENT_SCOPE)
endfunction()
